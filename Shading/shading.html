<!DOCTYPE HTML>
<head>
    <title>Phong Shading</title>
    <meta charset="utf-8" />
</head>


<script id="vs-src" type="x-shader/x-vertex">
    attribute vec3 aVertexPosition;
    attribute vec3 normal;
    uniform mat4 uMVMatrix;
    uniform mat4 uProjectionMatrix;
    uniform mat4 normalMat;
    varying vec3 normalInterp;
    varying vec3 vertPos;
    
    varying vec4 vColor;
    
    void main(void) {
        vec4 vertPos4 = uMVMatrix * vec4(aVertexPosition, 1.0);
        vertPos = vec3(vertPos4) / vertPos4.w;
        normalInterp = vec3(normalMat * vec4(normal, 0.0));
        gl_Position = uProjectionMatrix * vertPos4;
    }
</script>

<script id="fs-src" type="x-shader/x-fragment">
    precision mediump float;
    varying vec3 normalInterp;  // Surface normal
    varying vec3 vertPos;       // Vertex position
    uniform float Ka;   // Ambient reflection coefficient
    uniform float Kd;   // Diffuse reflection coefficient
    uniform float Ks;   // Specular reflection coefficient
    uniform float shininessVal; // Shininess
    // Material color
    uniform vec3 ambientColor;
    uniform vec3 diffuseColor;
    uniform vec3 specularColor;
    uniform vec3 lightPos; // Light position

    void main() {
        vec3 N = normalize(normalInterp);
        vec3 L = normalize(lightPos - vertPos);

        // Lambert's cosine law
        float lambertian = max(dot(N, L), 0.0);
        float specular = 0.0;
        if(lambertian > 0.0) {
            vec3 R = reflect(-L, N);      // Reflected light vector
            vec3 V = normalize(-vertPos); // Vector to viewer
            // Compute the specular term
            float specAngle = max(dot(R, V), 0.0);
            specular = pow(specAngle, shininessVal);
        }
        gl_FragColor = vec4(Ka * ambientColor +
                      Kd * lambertian * diffuseColor +
                      Ks * specular * specularColor, 1.0);
    }

</script>

<script src="gl-matrix-min.js"></script>
<script type="text/javascript" src="webgl-utils.js"></script>
<script src="shading.js"></script>

<body onload="startup();">
    <canvas id="myCanvas" width="500" height="500"></canvas>
</body>